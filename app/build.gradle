import java.text.SimpleDateFormat

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.1.0'
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.+'
    }
}

apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'

def gitSha() {
    return '"' + 'git rev-parse --short HEAD'.execute().text.trim() + '"'
}

def buildTime() {
    def df = new SimpleDateFormat("yyyy-MM-dd'T'HH:mm'Z'")
    df.setTimeZone(TimeZone.getTimeZone("UTC"))
    return '"' + df.format(new Date()) + '"'
}

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "nu.wasis.geotracker"
        minSdkVersion 15
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
        buildConfigField "String", "GIT_SHA", gitSha()
        buildConfigField "String", "BUILD_TIME", buildTime()
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "GIT_SHA", gitSha()
            buildConfigField "String", "BUILD_TIME", buildTime()
        }
        debug {
            buildConfigField "String", "GIT_SHA", gitSha()
            buildConfigField "String", "BUILD_TIME", buildTime()
        }
    }

    sourceSets {
        main {
            java {
                srcDir 'src'
                srcDir 'src-gen'
            }
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:22.0.0'
    compile files('libs/Android-SimpleLocation.jar')
    compile 'de.greenrobot:greendao:1.3.7'
    compile 'de.greenrobot:eventbus:2.4.0'
    compile 'net.danlew:android.joda:2.7.2'
    compile 'com.google.code.gson:gson:2.3.1'
}
